buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.21.1"
    }
}

plugins {
    id 'java-library'
    id 'com.github.ben-manes.versions' version '0.27.0'
    id 'maven-publish'
    id 'signing'
    id 'io.codearte.nexus-staging' version '0.21.1'
}

repositories {
	mavenCentral()
	mavenLocal()
}

nexusStaging {
	username = "$ossrhUsername"
	password = "$ossrhPassword"
}

dependencies {
    api 'io.lettuce:lettuce-core:5.2.1.RELEASE'
	compileOnly 'org.projectlombok:lombok:1.18.10'
	annotationProcessor 'org.projectlombok:lombok:1.18.10'
	testImplementation 'junit:junit:4.12'
	testImplementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-csv:2.10.1'
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = "$ossrhUsername"
                password = "$ossrhPassword"
            }
        }
    }
    
    publications {
        mavenJava(MavenPublication) {
            groupId 'com.redislabs'
            artifactId 'lettusearch'
            from components.java

            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'LettuSearch'
                packaging = 'jar'
                description = 'Java client for RediSearch based on Lettuce'
                url = 'https://github.com/RediSearch/lettusearch'

		        scm {
		          connection = 'scm:git:git://github.com/RediSearch/lettusearch.git'
		          developerConnection = 'scm:git:git@github.com:RediSearch/lettusearch.git'
		          url = 'https://github.com/RediSearch/lettusearch'
		        }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'jruaux'
                        name = 'Julien Ruaux'
                    }
                }
            }
        }
    }
}

signing {
	sign publishing.publications.mavenJava
}

group = 'com.redislabs'
archivesBaseName = 'lettusearch'
version = '1.9.2'
description = 'LettuSearch'
sourceCompatibility = '1.8'