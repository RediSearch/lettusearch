plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'com.github.ben-manes.versions' version '0.27.0'
}

repositories {
	mavenCentral()
	mavenLocal()
}

dependencies {
    api 'io.lettuce:lettuce-core:5.2.1.RELEASE'
	compileOnly 'org.projectlombok:lombok:1.18.10'
	annotationProcessor 'org.projectlombok:lombok:1.18.10'
	testImplementation 'junit:junit:4.12'
	testImplementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-csv:2.10.1'
}

bintray {
    user = bintrayUser
    key = bintrayKey
    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = 'lettusearch'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/RediSearch/lettusearch.git'
    }
    version {
    	gpg {
    		sign = true
    	}
    	mavenCentralSync {
    		sync = true
    	}
    }
    publish = true
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'com.redislabs'
            artifactId 'lettusearch'
            from components.java

            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'LettuSearch'
                packaging = 'jar'
                description = 'Java client for RediSearch based on Lettuce'
                url = 'https://github.com/RediSearch/lettusearch'

		        scm {
		          connection = 'scm:git:git://github.com/RediSearch/lettusearch.git'
		          developerConnection = 'scm:git:git@github.com:RediSearch/lettusearch.git'
		          url = 'https://github.com/RediSearch/lettusearch'
		        }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'jruaux'
                        name = 'Julien Ruaux'
                    }
                }
            }
        }
    }
}

group = 'com.redislabs'
archivesBaseName = 'lettusearch'
version = '1.9.3'
description = 'LettuSearch'
sourceCompatibility = '1.8'